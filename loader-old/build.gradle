plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" apply false

}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

task packCore(type: ShadowJar) {
    archiveBaseName.set('sakiko-old')

    configurations = [
            project(':core-old').configurations.runtimeClasspath,
            project(':extension-old').configurations.runtimeClasspath
    ]
    from project(':core-old').sourceSets.main.output
    from project(':extension-old').sourceSets.main.output

    archiveClassifier.set('core')
}

task packAll(type: Jar) {
    dependsOn ':loader-old:copyApi'

    archiveBaseName.set('sakiko-old')

    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    archiveClassifier.set('launcher')

    from(files(tasks.packCore.outputs)) { conf ->
        conf.into('io/github/karlatemp/jvmhook/launcher')
        conf.rename { 'classes.jar' }
    }

    manifest {
        attributes([
                'Premain-Class': 'io.github.karlatemp.jvmhook.launcher.Launcher'
        ])
    }
}

task copyApi(type: Copy) {
    into(new File(buildDir, 'libs'))
    from(project(':api-old').tasks.jar.outputs.files)
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '20.1.0'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}